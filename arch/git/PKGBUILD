# Maintainer: Pritunl <contact@pritunl.com>

pkgname=pritunl-git
_pkgname=pritunl
pkgver=0.0.0
pkgrel=1
pkgdesc="Enterprise VPN Server (git version)"
arch=("any")
license=("custom")
url="https://github.com/${_pkgname}/${_pkgname}"
depends=(
    "python"
    "python2"
    "python2-flask"
    "python2-pyopenssl"
    "python2-pymongo"
    "net-tools"
    "iproute2"
    "openvpn"
    "mongodb"
)
makedepends=(
    "git"
    "python2-distribute"
    "python2-flask"
    "python2-pyopenssl"
    "python2-pymongo"
)
provides=("${pkgname}")
conflicts=("${_pkgname}")
install=${_pkgname}.install
source=("git+${url}.git")
sha256sums=("SKIP")
options=("emptydirs")
backup=(
    "etc/${_pkgname}.conf"
    "var/lib/${_pkgname}/${_pkgname}.db"
    "var/log/${_pkgname}.log"
    "var/log/${pkgname}.log.1"
)

pkgver() {
  cd "$srcdir/$_pkgname"
  git log -1 --format=%at
}

build() {
    cd "${srcdir}/${_pkgname}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}"
    mkdir -p "${pkgdir}/var/lib/${_pkgname}"
    python2 setup.py install --root="${pkgdir}" --prefix=/usr --no-upstart
}
